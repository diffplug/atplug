plugins {
	id 'com.diffplug.blowdryer'
	id 'com.diffplug.spotless-changelog'
}

spotlessChangelog {
	tagMessage '{{changes}}'
	runAfterPush "gh release create release/{{version}} --title 'v{{version}}' --notes-from-tag"
}

repositories { mavenCentral() }

group = 干.proj('maven_group', 'the maven group, recommend com.diffplug')
allprojects {
	apply from: 干.file('base/changelog.gradle')
}
apply from: 干.file('base/sonatype.gradle')

String VER_JUNIT_JUPITER = '5.13.0'
subprojects {
	apply from: 干.file('base/java.gradle')
	apply from: 干.file('base/kotlin.gradle')
	apply from: 干.file('spotless/java.gradle')
	apply plugin: 'java-library'

	repositories { mavenCentral() }

	dependencies {
		testImplementation "org.junit.jupiter:junit-jupiter:${VER_JUNIT_JUPITER}"
		testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	}
	if (it.name == 'atplug-runtime') {
		ext.maven_name = 'AtPlug runtime'
		apply plugin: 'kotlinx-serialization'
		dependencies {
			implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.1"
		}
	} else if (it.name == 'atplug-test-harness') {
		ext.maven_name='AtPlug test harness'
		dependencies {
			compileOnly "org.junit.jupiter:junit-jupiter-api:5.13.0"
			implementation "org.jetbrains.kotlin:kotlin-reflect:2.1.21"
			implementation project(':atplug-runtime')
		}
	} else if (it.name == 'atplug-plugin-gradle') {
		ext.maven_name='AtPlug metadata generation Gradle plugin'
		apply from: 干.file('base/gradle-plugin.gradle')
		dependencies {
			implementation "org.ow2.asm:asm:9.8"
			implementation 'com.diffplug.gradle:goomph:4.3.0'
			implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.21'
			testImplementation 'org.assertj:assertj-core:3.27.3'
		}
		tasks.named('test') {
			dependsOn ':atplug-runtime:jar', ':atplug-runtime:testClasses'
		}
	} else {
		throw new IllegalArgumentException("Unexpected project " + it.name)
	}
	ext.maven_desc = ext.maven_name

	tasks.named('test', Test) {
		useJUnitPlatform {}
	}

	// it all needs to get published and formatted
	apply from: 干.file('base/maven.gradle')
	apply from: 干.file('base/sonatype.gradle')
}
