plugins {
	id 'com.github.ben-manes.versions' version '0.17.0'	// dependencyUpdate
	id 'com.diffplug.gradle.spotless' version '3.7.0'	// https://github.com/diffplug/spotless/tree/master/plugin-gradle
	id 'com.jfrog.bintray' version '1.8.0'				// https://github.com/bintray/gradle-bintray-plugin
	id 'org.jdrupes.mdoclet' version '1.0.3'			// https://plugins.gradle.org/plugin/org.jdrupes.mdoclet
	id 'com.diffplug.gradle.eclipse.resourcefilters' version '3.9.0' // https://github.com/diffplug/goomph
}


/////////////////
// CODE FORMAT //
/////////////////
apply plugin: 'com.diffplug.gradle.spotless'
spotless {
	java {
		target fileTree('.') {
			include '**/*.java'
			exclude 'gradle/**'
		}
		licenseHeaderFile	rootProject.file('gradle/spotless.license.java')		// License header file
		importOrderFile		rootProject.file('gradle/spotless.importorder')			// An import ordering file, exported from Eclipse
		eclipse().configFile rootProject.file('gradle/spotless.eclipseformat.xml')	// XML file dumped out by the Eclipse formatter
	}
	format 'misc', {
		target fileTree('.') {
			include '**/.gitignore'
			include '**/*.gradle'
			include '**/*.md'
			include '**/*.sh'
		}
		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
	freshmark {
		target rootProject.fileTree('.') {
			include '**/*.md'
		}
		propertiesFile('gradle.properties')
	}
}

//////////////////////////
// ROOT ECLIPSE PROJECT //
//////////////////////////
eclipseResourceFilters {
	exclude().folders().name('gradle-plugin')
	exclude().folders().name('lib-runtime')
}

/////////////////
// SUBPROJECTS //
/////////////////
subprojects {
	////////////////
	// JAVA SETUP //
	////////////////
	apply plugin: 'java'
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
	sourceSets {
		main.java.srcDir 'src'
		test.java.srcDir 'test'
	}
	repositories {
		mavenCentral()
	}

	/////////////////////
	// ECLIPSE PROJECT //
	/////////////////////
	apply plugin: 'eclipse'
	eclipse {
		classpath {
			downloadSources true
			downloadJavadoc true
		}
	}
	// always create fresh projects
	tasks.eclipse.dependsOn(cleanEclipse)

	//////////////
	// FINDBUGS //
	//////////////
	apply plugin: 'findbugs'
	findbugs {
		toolVersion = '3.0.1'
		sourceSets = [sourceSets.main]	// don't check the test code
		ignoreFailures = false 	// bug free or it doesn't ship!
		reportsDir = file('build/findbugs')
		effort = 'max'			// min|default|max
		reportLevel = 'high'	// low|medium|high (low = sensitive to even minor mistakes)
		omitVisitors = []		// bugs that we want to ignore
	}
	// HTML instead of XML
	tasks.withType(FindBugs) {
		reports {
			xml.enabled = false
			html.enabled = true
		}
	}
	// we'll want the findbugs annotations (they don't have a 3.0.1 version)
	dependencies {
		compileOnly 'com.google.code.findbugs:annotations:3.0.1'
		compileOnly 'com.google.code.findbugs:jsr305:3.0.1'
	}
}
